
Function getGET(Request)
		
	RequestDate = CurrentDate();
	//RequestDateAsString = Format(RequestDate, "DF=yyyyMMddHHmmss");
	Timer = New Structure("StartTime, LastTime, Time, Stage", RequestDate, RequestDate, New Array, New Array);
	Timer.Time.Add(0);
	Timer.Stage.Add("Begin");
	
	ChassisNumber = Request.QueryOptions.Get("chassisnumber");
	Type = Request.QueryOptions.Get("type");
	
	Response = New HTTPServiceResponse(200);
		
	BodyStructureResponse = New Structure;
	BodyStructureResponse.Insert("Error", "");
	BodyStructureResponse.Insert("VT", New ValueTable);
	BodyStructureResponse.Insert("WebResult", "");
	
	If ChassisNumber = Undefined Then
		
		//ERROR
		BodyStructureResponse.Error = "Chassis number is not provided.";	
		
	Else
		
		Try 
			
			//WORK
			//ChassisNumber = "JM7GH32F891120698";
			BodyStructureResponse.VT = Service.GetHistory(ChassisNumber, RequestDate, Timer);
			
			WebResult = "Chassis Number: " + ChassisNumber;
			
			If BodyStructureResponse.VT.Count() = 0 Then
				
				WebResult = WebResult + Chars.LF + "No accident history found.";	
				
			Else
				
				For Each Record In BodyStructureResponse.VT Do
					
					WebResult = WebResult + Chars.LF + Record.ReportDate + " " + Record.Emirate;	
					
				EndDo;
				
			EndIf;
			
			BodyStructureResponse.WebResult = WebResult;
			
		Except
			
			//ERROR
			ErrorDescription = ErrorDescription();
			Commentaries = "" + ChassisNumber + Chars.LF + Format(RequestDate, "DF=yyyyMMddHHmmss") + Chars.LF + ErrorDescription; 
			WriteLogEvent("HistoryGet", EventLogLevel.Error, , , Commentaries, );
			BodyStructureResponse.Insert("Error", "Please try again late.");
			
		EndTry;
		
	EndIf;
	
	BodyAsString = Undefined;
	
	If Type = "web" Then
		
		If ValueIsFilled(BodyStructureResponse.Error) Then
			
			BodyAsString = BodyStructureResponse.Error;
			
		Else
			
			BodyAsString = BodyStructureResponse.WebResult;
			
		EndIf;		
		
	ElsIf Type = "mob" Then
		
		BodyAsString = Serialization.GetTransferString(BodyStructureResponse);
		BodyAsString = Serialization.RemoveValueTag(BodyAsString);
		
	EndIf;		
	
	If Not BodyAsString = Undefined Then
		
		Response.SetBodyFromString(BodyAsString);
		
	EndIf;
	
	Timer.Time.Add(CurrentDate()-Timer.LastTime);
	Timer.LastTime = CurrentDate();
	Timer.Stage.Add("Finish");

	Str = ""+Timer.StartTime;
	
	
	For i = 1 To Timer.Time.Count() Do
		
		Str = Str + Chars.LF + "Time: " + Timer.Time[i-1] + " " + Timer.Stage[i-1];
		
	EndDo;
	
	WriteLogEvent("HistoryGet", EventLogLevel.Information, , , Str, );
	Return Response;
	
EndFunction
